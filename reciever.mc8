***** DeedsMcE Project File *****
* If you read this message, you need to install the last version of Deeds! *
FVR 3
SYS 1
CPU 1
FCK 2
ROM 4
RAM 4
PIA 0
PIB 1
PIC 2
PID 3
PIE 4
PIF 5
PIG 6
PIH 7
POA 0
POB 1
POC 2
POD 3
POE 4
POF 5
POG 6
POH 7
INT 007 SRC 002 PRT 000 TMD 001 TIN 019 EIN 003 
INT 006 SRC 002 PRT 000 TMD 001 TIN 019 EIN 003 
INT 005 SRC 002 PRT 000 TMD 001 TIN 019 EIN 003 
INT 004 SRC 002 PRT 000 TMD 001 TIN 019 EIN 003 
INT 003 SRC 002 PRT 000 TMD 001 TIN 019 EIN 003 
INT 002 SRC 002 PRT 000 TMD 001 TIN 019 EIN 003 
INT 001 SRC 002 PRT 000 TMD 001 TIN 019 EIN 003 
INT 000 SRC 002 PRT 000 TMD 001 TIN 019 EIN 003 
******* Project File END ********
;=========================================================
; DMC8 - SOS RECEIVER
;=========================================================

DATAP       EQU 01h         ; IB Input Port (Serial Receiver)
LED         EQU 02h         ; OC Output Port (Received Data Output)
TICK        EQU 0E000h      ; Timer Counter
STATE       EQU 0E001h      ; Blink Sequence Counter (0, 1, 2)
INCOMING    EQU 0E002h      ; Mode: 0=Idle, 1='S', 2='O'

;---------------- RESET VECTOR ---------------------------
            ORG     0000h
            JP      START

;---------------- INTERRUPT VECTOR -----------------------
            ORG     0038h           ; Timer Interrupt Address
            JP      TIMER_ISR
            
            ORG     0100h   

;---------------- INITIALIZATION -------------------------
START:      LD      SP, 0FFFFh      ; Stack Pointer Security Setting
            LD      A, 00h
            OUT     (LED), A        ; Turn LED OFF
            LD      (TICK), A       ; Tick = 0
            LD      (STATE), A      ; State = 0
            LD      (INCOMING), A   ; Mode = 0 (Idle)
            EI                      ; Enable Interrupts (Start Timer)

;---------------- MAIN LOOP ------------------------------
MAIN:       HALT
            JP      MAIN            ; Executed in the main loop

;---------------- TIMER INTERRUPT ------------------------
TIMER_ISR:  PUSH    AF              ; Save Processor State

            ; 1. CHECK CURRENT STATUS
            ; If we are already blinking a letter (INCOMING != 0),
            ; Do NOT read new data yet. Continue blinking.
            LD      A, (INCOMING)
            CP      00h
            JP      NZ, BLINK_ROUTINE ; If busy, jump to routine

            ; 2. IF IDLE, CHECK FOR NEW DATA
            IN      A, (DATAP)      ; Read Data (Auto Handshake)
            
            CP      53h             ; Is it 'S'?
            JP      Z, SET_S_MODE
            CP      4Fh             ; Is it 'O'?
            JP      Z, SET_O_MODE
            JP      EXIT_ISR        ; If no valid data, exit

;---------------- MODE SETTERS ---------------------------
SET_S_MODE: LD      A, 01h          ; Set Mode = 1 ('S')
            LD      (INCOMING), A
            LD      A, 00h          ; Reset Counters
            LD      (TICK), A
            LD      (STATE), A
            JP      BLINK_ROUTINE

SET_O_MODE: LD      A, 02h          ; Set Mode = 2 ('O')
            LD      (INCOMING), A 
            LD      A, 00h          ; Reset Counters
            LD      (TICK), A
            LD      (STATE), A

;---------------- BLINK ROUTINE --------------------------
BLINK_ROUTINE:            
            ; Check if Mode is active (If 0, do nothing)
            LD      A, (INCOMING)
            CP      00h
            JP      Z, EXIT_ISR

            ; 2. INCREMENT TICK (Same logic as Sender)
            LD      A, (TICK)
            INC     A
            LD      (TICK), A

            ; 3. EXECUTE LOGIC
            LD      A, (INCOMING)
            CP      01h             ; Handle 'S'
            JP      Z, HANDLE_S
            CP      02h             ; Handle 'O'
            JP      Z, HANDLE_O
            JP      EXIT_ISR

;---------------- STATE HANDLERS -------------------------
HANDLE_S:   LD      A, (STATE)
            CP      03h             ; 3 Blinks done?
            JP      NC, RESET_ALL   ; If yes, finish
            JP      DOT_LOGIC       ; Else, do DOT

HANDLE_O:   LD      A, (STATE)
            CP      03h
            JP      NC, RESET_ALL
            JP      DASH_LOGIC      ; Do DASH

;---------------- LOGIC BLOCKS ---------------------------
DOT_LOGIC:  LD      A, (TICK)
            CP      04H             ; Total Time (Short)
            JP      NC, NEXT_STATE
            CP      02H             ; ON Time (Short)
            JP      C, LED_ON    
            JP      LED_OFF      

DASH_LOGIC: LD      A, (TICK)
            CP      08H             ; Total Time (Long)
            JP      NC, NEXT_STATE
            CP      06H             ; ON Time (Long)
            JP      C, LED_ON    
            JP      LED_OFF      

;---------------- SUB FUNCTIONS --------------------------
LED_ON:     IN      A, (LED)        ; Read PORT C
            OR      01h             ; Turn on the LED
            OUT     (LED), A        ; Write to PORT C
            JP      EXIT_ISR             

LED_OFF:    IN      A, (LED)        ; Read PORT C
            AND     0FEh            ; Turn off the LED
            OUT     (LED), A        ; Write to PORT C
            JP      EXIT_ISR

NEXT_STATE: IN      A, (LED)        ; Read PORT C
            AND     0FEh            ; Turn off the LED
            OUT     (LED), A        ; Write to PORT C
            
            LD      A, 00h
            LD      (TICK), A       ; Reset TICK
            
            LD      A, (STATE)      ; Increase STATE
            INC     A
            LD      (STATE), A 
            JP      EXIT_ISR 

RESET_ALL:  IN      A, (LED)        ; Read PORT C
            AND     0FEh            ; Turn off the LED
            OUT     (LED), A        ; Write PORT C
            
            LD      A, 00h
            LD      (INCOMING), A   ; INCOMING = 0 (Ready for new data)
            LD      (STATE), A      ; STATE = 0
            LD      (TICK), A       ; TICK = 0
            JP      EXIT_ISR

EXIT_ISR:   POP     AF
            EI                      ; Re-enable Timer Interrupt
            RET
